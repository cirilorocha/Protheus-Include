/*
	Header : tlpp-object.th
	Copyright (c) 2019, TOTVS SA
	All rights reserved.
*/

#ifndef TLPP_OBJECT_TH
#define TLPP_OBJECT_TH

//#define _FuncType_

#xtranslate DEFAULT <uVar1> := <uVal1> ;
      [, <uVarN> := <uValN> ] => ;
     <uVar1> := If( ValType(<uVar1>) == 'U', <uVal1>, <uVar1> ) ;;
   [ <uVarN> := If( ValType(<uVarN>) == 'U', <uValN>, <uVarN> ); ]

// Os comandos abaixo foram substituidos pelo xtranslate em carater emergencial (sistemico)
// Analisar com calma a precedencia entre os dois comandos DTTLPP-1664
//#xcommand DEFAULT <Desc> [, <DescN> ]      => ;
//                  __DFT__( <Desc> ) [ ; __DFT__( <DescN> ) ]

//#xtranslate __DFT__( <Var> := <Dft> ) => ;
//            IIf( ValType(<Var>) == 'U', <Var> := <Dft>, )

//#xtranslate __DFT__( <Var> = <Dft> )  => ;
//            __DFT__( <Var> := <Dft> )

#xtranslate BYNAME <V> [, <VN> ]     => ::<V> := <V> [; ::<VN> := <VN> ]
#xtranslate BYNAME <V> DEFAULT <Val> => ::<V> := BYDEFAULT <V>, <Val>
#xtranslate BYNAME <V> IFNONIL       => ;
                            if <V> != NIL ;;
                                ::<V> := <V> ;;
                            end
#xtranslate BYDEFAULT <V>, <Val>     => if( <V> == NIL, <Val>, <V> )

#xcommand CLASS <ClsNam>   ;
		[ <from: INHERIT FROM, INHERIT, FROM, OF> [<NSpace>.]<SupCls> ];
		=> ;
		_ObjNewClass( <ClsNam> , [<NSpace>.][<SupCls>]) 


#xcommand CLASS <ClsNam>   ;
		[ <from: INHERIT FROM, INHERIT, FROM, OF> [<NSpace>.]<SupCls> ];
		<implements: IMPLEMENTS> [<NSpace2>.]<SupCls2> [, [<NSpace3>.]<SupCls3> ][, [<NSpace4>.]<SupCls4> ][, [<NSpace5>.]<SupCls5> ][, [<NSpace6>.]<SupCls6> ];
		=> ;
		_ObjNewClass( <ClsNam> , [<NSpace>.][<SupCls>] , [<NSpace2>.][<SupCls2>] , [<NSpace3>.][<SupCls3>], [<NSpace4>.][<SupCls4>], [<NSpace5>.][<SupCls5>], [<NSpace6>.][<SupCls6>]) 

		
#xcommand INTERFACE <ClsNam>   ;
		=> ;
		_ObjNewInterface( <ClsNam> ) 
		
		
#xcommand CLASS <ClsNam> CAMELCASE NAME <ClsSerial>;
        [ <from: INHERIT FROM, INHERIT, FROM, OF> <SupCls> ];
        => ;
        _ObjNewClass( <ClsNam> , [<SupCls>] ) ;;
        _ObjClassData( DESCRIPTION_SERIALIZE_<ClsNam>, string, , <ClsSerial> )

#xcommand CLASS <ClsNam> CAMELCASE;
        [ <from: INHERIT FROM, INHERIT, FROM, OF> <SupCls> ];
        => ;
        _ObjNewClass( <ClsNam> , [<SupCls>] ) ;;
        _ObjClassData( DESCRIPTION_SERIALIZE_<ClsNam>, string, , \"<ClsNam>\" )

#xtranslate CREATE CLASS <*ClsHead*> =>  CLASS <ClsHead>

#xcommand _GEN_DATA_ <vt>, <Vrs,...> [ AS <Typ,...> ]   ;
         [ <scp: PUBLIC, EXPORT, READONLY, PROTECTED, LOCAL, HIDDEN> ] ;
         [ <dft: DEFAULT, INIT> <uData> ]   ;
         [ USER DATA <uUserData> ] ;
         =>  ;
         _ObjAddMet( nClassH, __SCOPE__ [ <scp> ], [ \{ _AsUppLst_( <Typ> ) \} ] ,;
                    <vt>, [ <uData> ], _AsStrLst_( <Vrs> ) ) ;
         [ ; ObjSetUserData( nClassH, <uUserData>, _AsStrLst_( <Vrs> ) ) ]

#xcommand _GEN_DATA_ <vt>, <Vrs,...> [ AS <Typ,...> ]   ;
         [ <scp: PUBLIC, EXPORT, READONLY, PROTECTED, LOCAL, HIDDEN> ] ;
         [ INSTANTIATE <uData,...> ]    ;
         [ USER DATA <uUserData> ] ;
         =>  ;
         _ObjAddMet( nClassH, __SCOPE__ [ <scp> ], [ \{ _AsUppLst_( <Typ> ) \} ] ,;
                    <vt>, [ _ObjInsDat( \{|Self| <uData> \} )], _AsStrLst_( <Vrs> ) ) ;
         [ ; ObjSetUserData( nClassH, <uUserData>, _AsStrLst_( <Vrs> ) ) ]

#xcommand VAR <uVar> [AS <Typ>] ;
				[ <Scp: PUBLIC, EXPORT, READONLY, PROTECTED, LOCAL, HIDDEN> ] ;
				[ <Dft: DEFAULT, INIT> <uData> ] ;
				=> _ObjClassData( <uVar>, [<Typ>], [<Scp>], [<uData>] )
#xcommand INSTVAR <uVar> [AS <Typ>] ;
				[ <Scp: PUBLIC, EXPORT, READONLY, PROTECTED, LOCAL, HIDDEN> ] ;
				[ <Dft: DEFAULT, INIT> <uData> ] ;
				=> _ObjClassData( <uVar>, [<Typ>], [<Scp>], [<uData>] )
#xcommand DATA <uVar> [AS <Typ>] ;
				[ <Scp: PUBLIC, EXPORT, READONLY, PROTECTED, LOCAL, HIDDEN> ] ;
				[ <Dft: DEFAULT, INIT> <uData> ] ;
				=> _ObjClassData( <uVar>, [<Typ>], [<Scp>], [<uData>] )

#xcommand PUBLIC DATA <uVar> [AS <Typ>] ;
[ <Dft: DEFAULT, INIT> <uData> ] ;
=> _ObjClassData( <uVar>, [<Typ>], "PUBLIC", [<uData>] )

#xcommand PRIVATE DATA <uVar> [AS <Typ>] ;
[ <Dft: DEFAULT, INIT> <uData> ] ;
=> _ObjClassData( <uVar>, [<Typ>], "PRIVATE", [<uData>] )
	
#xcommand PROTECTED DATA <uVar> [AS <Typ>] ;
[ <Dft: DEFAULT, INIT> <uData> ] ;
=> _ObjClassData( <uVar>, [<Typ>], "PROTECTED", [<uData>] )

#xcommand STATIC DATA <uVar> [AS <Typ>] ;
[ <Dft: DEFAULT, INIT> <uData> ] ;
=> _ObjClassData( <uVar>, [<Typ>], "STATIC", [<uData>] )
	
#xcommand CAMELCASE DATA <uVar> [AS <Typ>] ;
        [ <Scp: PUBLIC, EXPORT, READONLY, PROTECTED, LOCAL, HIDDEN> ] ;
        [ <Dft: DEFAULT, INIT> <uData> ] ;
        => _ObjClassData( <uVar>, [<Typ>], [<Scp>], [<uData>] ) ;;
        _ObjClassData( DESCRIPTION_SERIALIZE_<uVar>, string, , \"<uVar>\" )

#xcommand CAMELCASE DATA <uVar> [AS ARRAY OF <Typ>] ;
        [ <Scp: PUBLIC, EXPORT, READONLY, PROTECTED, LOCAL, HIDDEN> ] ;
        [ <Dft: DEFAULT, INIT> <uData> ] ;
        => _ObjClassData( <uVar>, [arrayof_<Typ>], [<Scp>], [<uData>] ) ;;
        _ObjClassData( DESCRIPTION_SERIALIZE_<uVar>, string, , \"<uVar>\" )

#xcommand CLASSVAR <uVar> [AS <Typ>] ;
				[ <Scp: PUBLIC, EXPORT, READONLY, PROTECTED, LOCAL, HIDDEN> ] ;
				[ <Dft: DEFAULT, INIT> <uData> ] ;
				=> _ObjClassData( <uVar>, [<Typ>], [<Scp>], [<uData>] )
				
#xcommand CLASSDATA <uVar> [AS <Typ>] ;
				[ <Scp: PUBLIC, EXPORT, READONLY, PROTECTED, LOCAL, HIDDEN> ] ;
				[ <Dft: DEFAULT, INIT> <uData> ] ;
				=> _ObjClassData( <uVar>, [<Typ>], [<Scp>], [<uData>] )

#xcommand __METHOD__ <Met> [ <scp: PUBLIC, EXPORT, LOCAL, HIDDEN> ] [ <ctor: CONSTRUCTOR> ] => ;
		_ObjClassMethod(_AsName_( <Met> ),_AsParms_([<Met>]), [<scp>])

#xcommand _GEN_METHOD_ <Met> [,<MetN> ] [<*x*>] =>  ;
          __METHOD__  <Met> [,<MetN> ]  [<x>]

#xcommand _GEN_METHOD_ <Met> VIRTUAL [<*x*>] => __METHOD__ <Met>:_VIRTUAL_ [<x>]

#xcommand ERROR HANDLER <cFunc>  => ;
          _ObjAddMet( nClassH, , .f., 5, \{|| _AsName_( <cFunc> )() \} )

#xcommand ERROR HANDLER <cFunc> <code: EXTERN, CFUNC, CMETHOD> => ;
          EXTERNAL _AsName_( <cFunc> ) ;;
          _ObjAddMet( nClassH, , .f., 5, _AsStr_( <cFunc> ) )

#xcommand _GEN_METHOD_ <cFunc> ERROR HANDLER [ <x> ] => ;
          ERROR HANDLER <cFunc> [ <x> ]


#xcommand _GEN_METHOD_ <cMeth> OPERATOR <cOp> => ;
          _ObjAddMet( nClassH, , .f., 6, <(cOp)>, \{|| _AsName_( <cMeth> )() \} )

#xcommand _GEN_METHOD_ <cMeth> ALIAS OF <cMsg> => ;
          _ObjAddMet( nClassH, _AsStr_( <cMeth> ), _AsStr_( <cMsg> ), 7 )

#xcommand  __ST__  <st: METHOD, MESSAGE, VAR, INSTVAR, DATA, CLASSVAR, CLASSDATA > <*x*> ;
                  => <st> <x>
#xcommand EXPORT  <*x*> => __ST__ <x> PUBLIC
#xcommand HIDE    <*x*> => __ST__ <x> HIDDEN
#xcommand PROTECT <*x*> => __ST__ <x> PROTECTED
#xcommand EXPORT:     =>   _DftScope( 0 )
#xcommand PUBLIC:     =>   EXPORT:
#xcommand PROTECTED:  =>   _DftScope( 1 )
#xcommand READONLY:   =>   PROTECTED:
#xcommand LOCAL:      =>   _DftScope( 2 )
#xcommand HIDDEN:     =>   LOCAL:

#xtranslate _MetTrans_( <Met> ) => ;
            _AsStr_( <Met> ), \{|| _AsName_( <Met> )() \}

#xtranslate _MetTrans_( <Met> = <udf> ) => ;
            _AsStr_( <Met> ), \{|| _AsName_( <udf> )() \}

#xtranslate _MetTrans_( <Met>:_VIRTUAL_ ) => ;
            _AsStr_( <Met> ), "_VIRTUAL_"

#xtranslate _MetTrans_( <Met>:_SETGET_ ) => ;
            _AsStr_( <Met> ), \{|| _AsName_( <Met> )() \}, ;
            "_" + _AsStr_( <Met> ), \{|| _AsName_( <Met> )() \}

#xtranslate _BlkTrans_( <Met> INLINE <code,...> ) => ;
            #<Met>, \{ | Self | <code> \}

#xtranslate _BlkTrans_( <Met>( [<prm,...>] ) INLINE <code,...> ) => ;
            #<Met>, \{ | Self [, <prm> ] | <code> \}

#xtranslate _BlkTrans_( <Met> BLOCK <code,...> ) => ;
            _AsStr_( <Met> ), <code>

#xtranslate _AsParms_( <itm>( [<prm,...>] ) )    =>  [(<prm>)] 

#xtranslate _AsFunc_( <itm> )                   => <itm>()
#xtranslate _AsFunc_( <itm>( [<prm,...>] ) )    =>  <itm>( [<prm>] )

#xtranslate _AsName_( <itm> )                   => <itm>
#xtranslate _AsName_( <itm>( [<prm,...>] ) )    => <itm>


#xtranslate _AsStr_( <itm> )                    => <(itm)>
#xtranslate _AsStr_( <itm>( [<prm,...>] ) )     => #<itm>
#xtranslate _AsUpp_( <itm> )                    => upper( _AsStr_( <itm> ) )

#xtranslate _AsStrLst_( <Typ> [, <TypN> ] )     => ;
                                    _AsStr_( <Typ> ) [, _AsStr_( <TypN> ) ]
#xtranslate _AsUppLst_( <Typ> [, <TypN> ] )     => ;
                                    _AsUpp_( <Typ> ) [, _AsUpp_( <TypN> ) ]

#xtranslate __SCOPE__                                => NIL
#xtranslate __SCOPE__ <scp: PUBLIC, EXPORT>          => 0
#xtranslate __SCOPE__ <scp: READONLY, PROTECTED>     => 1
#xtranslate __SCOPE__ <scp: LOCAL, HIDDEN>           => 2

#xtranslate :VIRTUAL => :_VIRTUAL_
#xtranslate :SETGET  => :_SETGET_

#xcommand ENDCLASS  =>                                ;
		_ObjEndClass( )  

#xcommand ENDINTERFACE  =>                                ;
		_ObjEndInterface( )  		

#xcommand END CLASS  => ENDCLASS
#xcommand END INTERFACE  => ENDINTERFACE

#xtranslate :Parent( <SupCls> ):<*M*> => :<SupCls>:<M>

#xtranslate :Super  => :Parent

#xtranslate ::      =>    Self:

#xcommand METHOD <Met> [ <scp: PUBLIC, EXPORT, LOCAL, HIDDEN> ][ <ctor: CONSTRUCTOR> ][ VIRTUAL ] => ;
		_ObjClassMethod(_AsName_( <Met> ),_AsParms_([<Met>]), [<scp>])
		
#xcommand PRIVATE METHOD <Met> [ <ctor: CONSTRUCTOR> ][ VIRTUAL ] => ;
		_ObjClassMethod(_AsName_( <Met> ),_AsParms_([<Met>]), "PRIVATE")
		
#xcommand PUBLIC METHOD <Met> [ <ctor: CONSTRUCTOR> ][ VIRTUAL ] => ;
		_ObjClassMethod(_AsName_( <Met> ),_AsParms_([<Met>]), "PUBLIC")
		
#xcommand PROTECTED METHOD <Met> [ <ctor: CONSTRUCTOR> ][ VIRTUAL ] => ;
		_ObjClassMethod(_AsName_( <Met> ),_AsParms_([<Met>]), "PROTECTED")

#xcommand STATIC METHOD <Met> [ <ctor: CONSTRUCTOR> ][ VIRTUAL ] => ;
		_ObjClassMethod(_AsName_( <Met> ),_AsParms_([<Met>]), "STATIC")
		
#xtranslate METHOD <Met> CLASS <clas> [ VIRTUAL ]=> ;
		Function ___<clas>_AsMet_(<Met>)
		
#xtranslate _AsMet_( <itm>( [<prm,...>] ) )  => ____<itm>( [<prm>] )

#endif /* TLPP_OBJECT_TH */
